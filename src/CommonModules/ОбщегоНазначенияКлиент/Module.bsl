
#Область Напоминания

Процедура ЗапланироватьНапоминание(Интервал) Экспорт
	
	ПодключитьОбработчикОжидания("Напомнить", Интервал, Истина);
	
КонецПроцедуры

Процедура ОбработатьНапоминание() Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияФормыНапоминаний", ОбщегоНазначенияКлиент);
	
	ОткрытьФорму("РегистрСведений.Напоминания.Форма.ФормаНапоминания",,,,,, Оповещение)
	
КонецПроцедуры

Процедура ПослеЗакрытияФормыНапоминаний(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ЗапланироватьНапоминание(300);
	
КонецПроцедуры

Процедура СоздатьОтложитьНапоминания(ОбъектыНапоминаний, ОповещениеОПереносеНапоминаний) Экспорт
	
	ПараметрыВводаДаты = Новый Структура();
	ПараметрыВводаДаты.Вставить("ОбъектыНапоминаний", ОбъектыНапоминаний);
	ПараметрыВводаДаты.Вставить("ОповещениеОПереносеНапоминаний", ОповещениеОПереносеНапоминаний);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаДаты", ОбщегоНазначенияКлиент, ПараметрыВводаДаты);
	
	ПоказатьВводДаты(Оповещение, НачалоМинуты(ТекущаяДата()) + 600);
	
КонецПроцедуры

Процедура ЗавершитьНапоминания(ОбъектыНапоминаний, ОповещениеОЗавершенииНапоминаний) Экспорт

	ОбщегоНазначенияВызовСервера.ЗавершитьНапоминания(ОбъектыНапоминаний);
	
	ВыполнитьОбработкуОповещения(ОповещениеОЗавершенииНапоминаний);
	
КонецПроцедуры

Процедура ПослеВводаДаты(Дата, ДополнительныеПараметры) Экспорт
	
	Если Дата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияВызовСервера.СоздатьОтложитьНапоминания(Дата, ДополнительныеПараметры.ОбъектыНапоминаний);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОПереносеНапоминаний);
	
КонецПроцедуры

#КонецОбласти

Функция НовыеПараметрыИзмененияОписания() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ОбъектОписания");
	Результат.Вставить("Описание");
	Результат.Вставить("ФормаВладелец");
	Результат.Вставить("ОповещениеОбИзменении");
	
	Возврат Результат;
	
КонецФункции

Процедура ИзменитьОписание(Параметры) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ОбъектОписания", Параметры.ОбъектОписания);
	
	Если ЗначениеЗаполнено(Параметры.Описание) Тогда
		ПараметрыФормы.Вставить("Ключ", Параметры.Описание);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Описания.Форма.ФормаЭлемента", ПараметрыФормы, Параметры.ФормаВладелец,,,, Параметры.ОповещениеОбИзменении);
	
КонецПроцедуры

Функция НовыеПараметрыВыбораОписания() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ОбъектОписания");
	Результат.Вставить("ФормаВладелец");
	Результат.Вставить("ОповещениеОВыборе");
	
	Возврат Результат;
	
КонецФункции

Процедура ВыбратьОписание(Параметры) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ОбъектОписания", Параметры.ОбъектОписания);
	
	ОткрытьФорму("Справочник.Описания.Форма.ФормаВыбораОписания", ПараметрыФормы, Параметры.ФормаВладелец,,,, Параметры.ОповещениеОВыборе);
	
КонецПроцедуры
