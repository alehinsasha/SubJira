
#Если Сервер Тогда

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("ДатаВремяНачала");
	Поля.Добавить("ДатаВремяОкончания");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДеньНедели = Формат(Данные.ДатаВремяНачала, "ДФ=ddd;");
	Дата = Формат(Данные.ДатаВремяНачала, "ДФ=dd.MM.yy;");
	ВремяНачала = Формат(Данные.ДатаВремяНачала, "ДФ=hh:mm;");
	ВремяОкончания = Формат(Данные.ДатаВремяОкончания, "ДФ=hh:mm;");
	
	Представление = СтрШаблон("%1 | %2 | %3 - %4", ДеньНедели, Дата, ВремяНачала, ВремяОкончания)
	
КонецПроцедуры

Функция ПредставлениеЗамера(Замер) Экспорт
	
	Затрачено = ?(ЗначениеЗаполнено(Замер.ДатаВремяОкончания), Замер.ДатаВремяОкончания, ТекущаяДатаСеанса()) - Замер.ДатаВремяНачала;
	Результат = СтрШаблон("[%1] %2", ОбщегоНазначенияКлиентСервер.ПредставлениеТрудозатрат(Затрачено), Замер.Владелец.Наименование);
	
	Возврат Результат;
	
КонецФункции

#КонецЕсли
